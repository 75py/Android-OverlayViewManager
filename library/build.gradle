/*
 * Copyright 2017 75py
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *    http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.library'
apply plugin: 'jacoco-android'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

jacoco {
    toolVersion '0.7.1.201405082137'
}
android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 27
        versionCode publish_version_code
        versionName publish_version_name

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles 'proguard-rules.pro'
        resourcePrefix 'overlayviewmanager_'
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
        }
    }
    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.includeAndroidResources = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'androidx.annotation:annotation:1.0.0'
    implementation 'androidx.fragment:fragment:1.0.0'

    compileOnly 'com.jakewharton.timber:timber:4.7.1'
    lintChecks project(':lint')

    testImplementation 'junit:junit:4.12'
    testImplementation "org.mockito:mockito-core:2.13.0"
    testImplementation "org.robolectric:robolectric:3.6.1"
    testImplementation 'com.jakewharton.timber:timber:4.7.1'

    androidTestImplementation 'androidx.test.uiautomator:uiautomator:2.2.0-beta01'
    androidTestImplementation 'androidx.test:runner:1.1.0-alpha4'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.0-alpha4'
}

def libGroupId = 'com.nagopy.android'
def libraryId = 'overlayviewmanager'
def siteUrl = 'https://github.com/75py/Android-OverlayViewManager'

version = publish_version_name
group = libGroupId
bintray {
    user = project.hasProperty('bintrayUser') ? project.getProperty('bintrayUser') : ""
    key = project.hasProperty('bintrayKey') ? project.getProperty('bintrayKey') : ""
    //dryRun = project.hasProperty('dryRun') ? project.getProperty('dryRun') : true
    dryRun = false
    publish = true

    configurations = ['archives'] //When uploading configuration files

    pkg {
        repo = 'maven'
        name = libraryId
        userOrg = '75py'
        vcsUrl = 'https://github.com/75py/Android-OverlayViewManager.git'
        licenses = ['Apache-2.0']
        websiteUrl = siteUrl
        version {
            name = publish_version_name
            desc = 'Simple API for displaying overlay your views'
        }
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            //name libraryId
            url siteUrl
            packaging 'aar'
            groupId libGroupId
            artifactId libraryId
            version publish_version_name
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

afterEvaluate {
    javadoc.classpath += files(android.libraryVariants.collect { variant ->
        variant.javaCompile.classpath.files
    })
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
    archives javadocJar
    archives sourcesJar
}
